dependencies {
    developmentOnly "org.springframework.boot:spring-boot-devtools:${springBootVersion}"
}

def profiles = "dev"
if (project.hasProperty("no-liquibase")) {
    profiles += ",no-liquibase"
}
if (project.hasProperty("tls")) {
    profiles += ",tls"
}

springBoot {
    buildInfo {
        properties {
            time = null
        }
    }
}

bootRun {
    args = []
}

task webpack(type: YarnTask, dependsOn: 'yarn') {
    inputs.dir("../src/main/webapp/")
    inputs.files("yarn.lock")
    inputs.files("build.gradle")

    def webpackDevFiles = fileTree("../webpack//")
    webpackDevFiles.exclude("../webpack.prod.js")
    inputs.files(webpackDevFiles)

    outputs.dir("build/resources/main/static/")

    args = ["run", "webpack:build"]
    environment = [APP_VERSION: project.version]
}

processResources {
    inputs.property('version', version)
    inputs.property('springProfiles', profiles)
    filesMatching("**/application.yml") {
        filter {
            it.replace("#project.version#", version)
        }
    }
    filesMatching("**/bootstrap.yml") {
        filter {
            it.replace("#spring.profiles.active#", profiles)
        }
    }
}

processResources.dependsOn webpack
bootJar.dependsOn processResources
