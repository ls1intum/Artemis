import org.gradle.internal.os.OperatingSystem
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repo.spring.io/plugins-release" }
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}"
        classpath "io.spring.gradle:propdeps-plugin:0.0.10.RELEASE"
        // this allows us to find outdated dependencies via './gradlew dependencyUpdates'
        classpath "com.github.ben-manes:gradle-versions-plugin:0.22.0"
        //jhipster-needle-gradle-buildscript-dependency - JHipster will add additional gradle build script plugins here
    }
}

plugins {
    id "checkstyle"
    id "java"
    id "maven"
    id "idea"
    id "jacoco"
    id "com.google.cloud.tools.jib" version "1.4.0"
    id "com.gorylenko.gradle-git-properties" version "2.0.0"
    id "com.github.node-gradle.node" version "2.0.0"
    id "net.ltgt.apt-eclipse" version "0.21"
    id "net.ltgt.apt-idea" version "0.21"
    id "net.ltgt.apt" version "0.21"
    id "com.moowork.node" version "1.3.1"
//    id 'org.liquibase.gradle' version "2.0.1"
    id "com.github.ben-manes.versions" version "0.22.0"
    //jhipster-needle-gradle-plugins - JHipster will add additional gradle plugins here
    id "com.diffplug.gradle.spotless" version "3.24.0"
}

sourceCompatibility=12
targetCompatibility=12

apply plugin: 'org.springframework.boot'
apply plugin: 'propdeps'
apply plugin: "com.github.ben-manes.versions"

apply from: "gradle/docker.gradle"
//this enables us to invoke ./gradlew liquibaseDiffChangelog
apply from: 'gradle/liquibase.gradle'
//jhipster-needle-gradle-apply-from - JHipster will add additional gradle scripts to be applied here

if (project.hasProperty("prod")) {
    apply from: "gradle/profile_prod.gradle"
} else {
    apply from: "gradle/profile_dev.gradle"
}

if (project.hasProperty("zipkin")) {
    apply from: "gradle/zipkin.gradle"
}

if (project.hasProperty("war")) {
    apply from: "gradle/war.gradle"
}

idea {
    module {
        excludeDirs += files('node_modules')
    }
}

spotless {
    // allows to execute the code formatting commands ./gradlew spotlessApply and ./gradlew spotlessCheck
    java {
        target project.fileTree(project.rootDir) {
            include '**/*.java'
            exclude '**/src/main/java/de/tum/in/www1/artemis/service/connectors/BambooService.java', '**/src/main/java/de/tum/in/www1/artemis/config/SecurityConfiguration.java', '**/node_modules/**', '**/out/**', '**/repos/**', '**/build/**', '**/src/main/resources/templates/**'
        }
        importOrderFile 'artemis-spotless.importorder'
        removeUnusedImports()
        eclipse().configFile 'artemis-spotless-style.xml'
    }
}

// See https://virgo47.wordpress.com/2018/09/14/classpath-too-long-with-spring-boot-and-gradle/ for details
// https://github.com/jhipster/generator-jhipster/issues/9713
if (OperatingSystem.current().isWindows()) {
    task classpathJar(type: Jar) {
        inputs.files sourceSets.main.runtimeClasspath

        archiveName = "runboot-classpath.jar"
        doFirst {
            manifest {
                def classpath = sourceSets.test.runtimeClasspath.files
                attributes "Class-Path": classpath.collect {f -> f.toURI().toString()}.join(" ")
            }
        }
    }

    bootRun {
        classpath = classpathJar.outputs.files
    }
}

defaultTasks 'bootRun'

group = 'de.tum.in.www1.artemis'
version = '3.5.0'
description = 'Interactive Learning with Individual Feedback'

springBoot {
    mainClassName = 'de.tum.in.www1.artemis.ArtemisApp'
}

// Execute the test cases: ./gradlew executeTests

test {
    useJUnitPlatform()
    exclude "**/*IT*", "**/*IntTest*", "**/*CucumberIT*"

    // uncomment if the tests reports are not generated
    // see https://github.com/jhipster/generator-jhipster/pull/2771 and https://github.com/jhipster/generator-jhipster/pull/4484
    // ignoreFailures true
    reports.html.enabled = false
}

task integrationTest(type: Test) {
    description = "Execute integration tests."
    group = "verification"
    include "**/*IT*", "**/*IntTest*"
    exclude "**/*CucumberIT*"

    // uncomment if the tests reports are not generated
    // see https://github.com/jhipster/generator-jhipster/pull/2771 and https://github.com/jhipster/generator-jhipster/pull/4484
    // ignoreFailures true
    reports.html.enabled = false
}

check.dependsOn integrationTest
task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/tests")
    reportOn test
}

task integrationTestReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/tests")
    reportOn integrationTest
}

if (!project.hasProperty('runList')) {
    project.ext.runList = 'main'
}

configurations {
    providedRuntime
    implementation.exclude module: "spring-boot-starter-tomcat"
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    //jhipster-needle-gradle-repositories - JHipster will add additional repositories
}

dependencies {

    implementation fileTree(dir: 'libs', include: '*.jar')
    // https://mvnrepository.com/artifact/org.eclipse.jgit/org.eclipse.jgit
    implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.4.0.201906121030-r'
    // https://mvnrepository.com/artifact/net.sourceforge.plantuml/plantuml
    implementation group: 'net.sourceforge.plantuml', name: 'plantuml', version: '8059'
    implementation "org.imsglobal:basiclti-util:1.2.0"
    implementation "org.jasypt:jasypt:1.9.3"
    implementation "me.xdrop:fuzzywuzzy:1.2.0"
    implementation group: 'com.atlassian.bamboo', name: 'bamboo-specs', version: '6.8.1'
    implementation group: 'com.thoughtworks.qdox', name: 'qdox', version: '2.0-M10'
    implementation 'io.sentry:sentry-spring:1.7.26'

    // import JHipster dependencies BOM
    implementation platform("io.github.jhipster:jhipster-dependencies:${jhipster_dependencies_version}" )

    // Use ", version: jhipster_dependencies_version, changing: true" if you want
    // to use a SNAPSHOT release instead of a stable release
    implementation group: "io.github.jhipster", name: "jhipster-framework"
    implementation "org.springframework.boot:spring-boot-starter-cache"
    implementation "io.micrometer:micrometer-registry-prometheus:1.2.0"
    implementation "net.logstash.logback:logstash-logback-encoder:5.3"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-hppc:2.9.9"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.9"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-hibernate5:2.9.9"
    implementation "com.fasterxml.jackson.core:jackson-annotations:2.9.9"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.9.9"
    implementation "com.fasterxml.jackson.module:jackson-module-afterburner:2.9.9"
    implementation "javax.cache:cache-api"
    implementation "org.hibernate:hibernate-core"
    implementation "com.zaxxer:HikariCP"
    implementation "org.apache.commons:commons-lang3:3.9"
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.7'
    implementation "commons-io:commons-io"
    implementation "javax.transaction:javax.transaction-api:1.3"
    implementation "org.ehcache:ehcache"
    implementation "org.hibernate:hibernate-jcache"
    implementation "org.hibernate:hibernate-entitymanager"
    implementation "org.hibernate:hibernate-envers"
    implementation "org.hibernate.validator:hibernate-validator"
    implementation "org.liquibase:liquibase-core"
//    liquibaseRuntime "org.liquibase:liquibase-core"
//    liquibaseRuntime "org.liquibase.ext:liquibase-hibernate5:${liquibase_hibernate5_version}"
//    liquibaseRuntime sourceSets.main.implementationClasspath
    implementation "org.springframework.boot:spring-boot-loader-tools"
    implementation "org.springframework.boot:spring-boot-starter-mail"
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-aop"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation ("org.springframework.boot:spring-boot-starter-web") {
        exclude module: 'spring-boot-starter-tomcat'
    }
    implementation "org.springframework.boot:spring-boot-starter-undertow"
    implementation "org.springframework.boot:spring-boot-starter-websocket"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"

    implementation "org.springframework.ldap:spring-ldap-core"
    implementation "org.springframework.data:spring-data-ldap"

    implementation "org.springframework.security:spring-security-config"
    implementation "org.springframework.security:spring-security-data"
    implementation "org.springframework.security:spring-security-web"
    implementation "org.springframework.security:spring-security-messaging"
    implementation "io.jsonwebtoken:jjwt-api"
    runtimeOnly "io.jsonwebtoken:jjwt-impl"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson"
    implementation ("io.springfox:springfox-swagger2") {
        exclude module: 'mapstruct'
    }
    implementation "io.springfox:springfox-bean-validators"
    implementation "mysql:mysql-connector-java"
//    liquibaseRuntime "mysql:mysql-connector-java"
    implementation "org.mapstruct:mapstruct:${mapstruct_version}"

    implementation "org.zalando:problem-spring-web:0.24.0-RC.0"

    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstruct_version}"
    annotationProcessor "org.hibernate:hibernate-jpamodelgen:${hibernate_version}"
    annotationProcessor "org.glassfish.jaxb:jaxb-runtime:${jaxb_runtime_version}"
    annotationProcessor ("org.springframework.boot:spring-boot-configuration-processor:${spring_boot_version}") {
        exclude group: "com.vaadin.external.google", module: "android-json"
    }
    testImplementation ("org.springframework.boot:spring-boot-starter-test") {
        exclude module: "junit"
        exclude group: "com.vaadin.external.google", module: "android-json"
    }
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.springframework.boot:spring-boot-test"
    testImplementation "org.assertj:assertj-core:3.13.2"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.1.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
    testImplementation "org.mockito:mockito-core:3.0.0"
    testImplementation "org.hamcrest:hamcrest-library"
    testImplementation "com.h2database:h2"
    testImplementation 'org.awaitility:awaitility:3.1.6'
    //jhipster-needle-gradle-dependency - JHipster will add additional dependencies here
}

task cleanResources(type: Delete) {
    delete 'build/resources'
}

task executeTests (type: Exec) {
    commandLine './gradlew', 'test', '-x', 'webpackBuildDev', '-x', 'webpack'
}

// Taken from here: https://stackoverflow.com/questions/3963708/gradle-how-to-display-test-results-in-the-console-in-real-time
tasks.withType(Test) {
    testLogging {
        // set options for log level LIFECYCLE
        events TestLogEvent.FAILED,
            TestLogEvent.PASSED,
            TestLogEvent.SKIPPED
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events TestLogEvent.STARTED,
                TestLogEvent.FAILED,
                TestLogEvent.PASSED,
                TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_ERROR,
                TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

wrapper {
    gradleVersion = '5.5.1'
}

task stage(dependsOn: 'bootWar') {
}

if (project.hasProperty('nodeInstall')) {
    node {
        version = "${node_version}"
        npmVersion = "${npm_version}"
        yarnVersion = "${yarn_version}"
        download = true
    }
}

// Command to execute the JavaDoc checkstyle verification ./gradlew checkstyleMain
checkstyle {
    toolVersion = '6.16.1'
    configDir = new File('checkstyle')
    maxErrors = 100
}

task executeCheckstyle (type: Exec) {
    commandLine './gradlew', 'checkstyleMain', '-x', 'yarn_install', '-x', 'webpackBuildDev', '-x', 'webpack'
}

task buildJarForDocker (type: Exec) {
    commandLine './gradlew', 'build', '-x', 'webpackBuildDev', '-x', 'webpack', '-x', 'test'
}

