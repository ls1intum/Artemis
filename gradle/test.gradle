import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

// Taken from here: https://stackoverflow.com/questions/3963708/gradle-how-to-display-test-results-in-the-console-in-real-time
tasks.withType(Test).configureEach {

    jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
    // Required by the Weaviate Java client v6, which uses Gson's reflection-based serialization on internal java.lang classes.
    // See: https://github.com/weaviate/java-client?tab=readme-ov-file#gson-and-reflective-access-to-internal-jdk-classes
    // This is harmless: it only opens java.lang to classpath code within the same JVM process and does not expand the attack surface for remote exploits.
    jvmArgs += "--add-opens=java.base/java.lang=ALL-UNNAMED"

    // Pass weaviate server version to integration tests
    systemProperty 'weaviate.server.version', findProperty('weaviate_server_version')

    // Pass Docker image versions from .env to integration tests as system properties
    // (e.g. MYSQL_VERSION=9.6.0 becomes -Dmysql.version=9.6.0, usable in Spring YAML as ${mysql.version})
    def envFile = file("${rootDir}/.env")
    if (envFile.exists()) {
        envFile.readLines().each { line ->
            def trimmed = line.trim()
            if (trimmed && !trimmed.startsWith('#') && trimmed.contains('=')) {
                def parts = trimmed.split('=', 2)
                def key = parts[0].trim()
                def value = parts[1].trim()
                // Convert ENV_VAR_NAME to env.var.name for use as system/Spring properties
                systemProperty key.toLowerCase().replaceAll('_', '.'), value
            }
        }
    }

    // Forward Gradle command-line -D properties to the test JVM for database type selection
    // (e.g. -Dzonky.test.database.type=MYSQL selects MySQL testcontainers instead of H2)
    def dbType = System.getProperty('zonky.test.database.type')
    if (dbType) {
        systemProperty 'zonky.test.database.type', dbType
        // Override Hibernate dialect to match the selected test database.
        // Without this, H2Dialect from application.yml generates SQL incompatible
        // with MySQL/PostgreSQL (e.g. "dual cross join" references).
        switch (dbType.toUpperCase()) {
            case 'MYSQL':
                systemProperty 'spring.jpa.database-platform', 'org.hibernate.dialect.MySQLDialect'
                systemProperty 'spring.jpa.database', 'MYSQL'
                break
            case 'POSTGRES':
                systemProperty 'spring.jpa.database-platform', 'org.hibernate.dialect.PostgreSQLDialect'
                systemProperty 'spring.jpa.database', 'POSTGRESQL'
                break
            // H2 is the default from application.yml, no override needed
        }
    }

    // a collection to track failedTests
    ext.failedTests = []

    testLogging {
        // set options for log level LIFECYCLE
        events TestLogEvent.FAILED,
            TestLogEvent.PASSED,
            TestLogEvent.SKIPPED
        exceptionFormat = TestExceptionFormat.FULL
        showExceptions = true
        showCauses = true
        showStackTraces = true

        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat
    }

    afterTest { descriptor, result ->
        if (result.resultType == TestResult.ResultType.FAILURE) {
            var failedTest = "${descriptor.className}::${descriptor.name}"
            logger.debug("Adding " + failedTest + " to failedTests...")
            failedTests << [failedTest]
        }
    }

    afterSuite { suite, result ->
        if (!suite.parent) { // will match the outermost suite
            def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            def startItem = "|  ", endItem = "  |"
            def repeatLength = startItem.length() + output.length() + endItem.length()
            println("\n" + ("-" * repeatLength) + "\n" + startItem + output + endItem + "\n" + ("-" * repeatLength))
            // logs each failed test
            if (!failedTests.empty) {
                logger.lifecycle("Failed tests:")
                failedTests.each { failedTest ->
                    println("${failedTest}")
                }
            }
        }
    }
}

// Allow using in jacoco.gradle
ext {
    includedTestTags = System.getProperty("includeTags")
    includedTags = !includedTestTags ? new String[]{} : includedTestTags.split(",") as String[]
    includedModulesTag = System.getProperty("includeModules")
    includedModules = !includedModulesTag ? new String[]{} : includedModulesTag.split(",") as String[]

    runAllTests = includedTags.size() == 0 && includedModules.size() == 0
    BasePath = "de/tum/cit/aet/artemis"
}


// Execute the test cases: ./gradlew test
// Execute only architecture tests: ./gradlew test -DincludeTags="ArchitectureTest"
// Execute tests only for specific modules: ./gradlew test -DincludeModules="atlas". Using this flag, "includeTags" will be ignored.
test {
    if (runAllTests) {
        useJUnitPlatform()
        exclude "**/*IT*", "**/*IntTest*"
    } else if (includedModules.size() == 0) {
        // not running all tests, but not module-specific ones -> use tags
        useJUnitPlatform() {
            includeTags includedTags
        }
    } else {
        useJUnitPlatform()
        // Always execute "shared"-folder when executing module-specifc tests
        includedModules += "shared"
        filter { testFilter ->
            includedModules.each { val ->
                testFilter.includeTestsMatching("de.tum.cit.aet.artemis.$val.*")
            }
        }
    }
    testClassesDirs = testing.suites.test.sources.output.classesDirs
    classpath = testing.suites.test.sources.runtimeClasspath
    testLogging {
        events "FAILED", "SKIPPED"
    }
    testLogging.showStandardStreams = true
    reports.html.required = false
    minHeapSize = "2g" // initial heap size
    maxHeapSize = "6g" // maximum heap size
}

// Convenience tasks: ./gradlew testMysql -x webapp / ./gradlew testPostgres -x webapp
['Mysql': ['MYSQL', 'org.hibernate.dialect.MySQLDialect', 'MYSQL'],
 'Postgres': ['POSTGRES', 'org.hibernate.dialect.PostgreSQLDialect', 'POSTGRESQL']].each { name, config ->
    tasks.register("test${name}", Test) {
        description = "Run tests against ${name} (via Testcontainers)"
        group = 'verification'
        testClassesDirs = testing.suites.test.sources.output.classesDirs
        classpath = testing.suites.test.sources.runtimeClasspath
        useJUnitPlatform()
        exclude "**/*IT*", "**/*IntTest*"
        testLogging.showStandardStreams = true
        reports.html.required = false
        minHeapSize = "2g"
        maxHeapSize = "6g"
        systemProperty 'zonky.test.database.type', config[0]
        systemProperty 'spring.jpa.database-platform', config[1]
        systemProperty 'spring.jpa.database', config[2]
    }
}

// Dynamic generation of jacoco test report generation-/coverage verification-tasks (per-module)
apply from: "gradle/jacoco.gradle"
