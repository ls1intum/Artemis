/**
 * Parses the .env file in the project root and stores key-value pairs in ext.envProperties.
 *
 * This avoids duplicating .env parsing logic across build.gradle and test.gradle.
 * The raw keys preserve their original casing (e.g. MYSQL_VERSION).
 * Consumers are responsible for converting keys to their required format:
 * - build.gradle: ext.set(key.toLowerCase(), value)  → e.g. mysql_version
 * - test.gradle:  systemProperty key.toLowerCase().replaceAll('_', '.'), value  → e.g. mysql.version
 */
ext.envProperties = [:]
def envFile = file('.env')
if (envFile.exists()) {
    envFile.readLines().each { line ->
        def trimmed = line.trim()
        if (trimmed && !trimmed.startsWith('#') && trimmed.contains('=')) {
            def parts = trimmed.split('=', 2)
            def key = parts[0].trim()
            def value = parts[1].trim().replaceAll(/^(['"])(.*)\1$/, '$2')
            envProperties[key] = value
        }
    }
}
