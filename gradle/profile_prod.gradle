def profiles = "prod"
if (project.hasProperty("no-liquibase")) {
    profiles += ",no-liquibase"
}

springBoot {
    buildInfo()
}

bootRun {
    args = []
    // Required by the Weaviate Java client v6, which uses Gson's reflection-based serialization on internal java.lang classes.
    // See: https://github.com/weaviate/java-client?tab=readme-ov-file#gson-and-reflective-access-to-internal-jdk-classes
    // This is harmless: it only opens java.lang to classpath code within the same JVM process and does not expand the attack surface for remote exploits.
    jvmArgs = ["--add-opens=java.base/java.lang=ALL-UNNAMED"]
}

tasks.register("webapp_test", NpmTask) {
    dependsOn "npm_install"
    args = ["run", "webapp:test"]
}

tasks.register("webapp", NpmTask) {
    dependsOn "npm_install"
    args = ["run", "webapp:prod"]
    environment = [APP_VERSION: project.version]
}

processResources {
    inputs.property("version", version)
    inputs.property("springProfiles", profiles)
    filesMatching("**/application.yml") {
        filter {
            it.replace("#project.version#", version)
        }
        filter {
            it.replace("#spring.profiles.active#", profiles)
        }
    }
}

test.dependsOn webapp_test
processResources.dependsOn webapp
bootJar.dependsOn processResources
